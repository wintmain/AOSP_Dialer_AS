plugins {
    id 'com.android.application'
    // https://github.com/google/protobuf-gradle-plugin
    id "com.google.protobuf" version "0.9.4"
}

android {
    compileSdk 34

    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId "com.wintmain.dialer"
        // ERROR: .../res/mipmap-anydpi/ic_launcher.xml:
        // AAPT: error: <adaptive-icon> elements require a sdk version of at least 26.
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName 'V20231127'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/NOTICE', 'META-INF/LICENSE',
                         'META-INF/LICENSE.txt', 'META-INF/NOTICE.txt']
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.name.endsWith("debug")) {
                outputFileName = "AOSP_Dialer_AS_v${defaultConfig.versionName}_debug.apk"
            } else {
                outputFileName = "AOSP_Dialer_AS_v${defaultConfig.versionName}_release.apk"
            }
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }

    buildFeatures {
        aidl true
    }

    sourceSets {
        def prefix = 'src/main/java/com/wintmain/dialer/'
        main {
            // Add additional resource directories here
            res.srcDirs += [
                    prefix + 'about/res',
                    prefix + 'app/res',
                    'src/main/java/com/android/voicemail/impl/res',
                    'src/main/java/com/android/voicemail/impl/configui/res'
            ]

            proto {
                // In addition to the default 'src/main/proto'
                srcDir 'src/main/java/com/android/incallui/callconfiguration'
                srcDir 'src/main/java/com/android/contacts/common/widget'
                srcDir 'src/main/java/com/wintmain/dialer/logging'
                srcDir 'src/main/java/com/wintmain/dialer/dialercontact'

                // If proto has dependency, move to same folder.
                srcDir 'src/main/java/com/wintmain/proto'

                // In addition to the default '**/*.proto' (use with caution).
                // Using an extension other than 'proto' is NOT recommended,
                // because when proto files are published along with class files, we can
                // only tell the type of a file from its extension.
                include '**/*.protodevel'
            }
        }
        test {}
    }

    namespace 'com.wintmain.dialer'
}

dependencies {
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'me.leolin:ShortcutBadger:1.1.22'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.13.14'
    implementation 'com.googlecode.libphonenumber:geocoder:2.208'
    implementation 'io.grpc:grpc-core:1.56.0'
    implementation 'com.google.zxing:core:3.5.1'
    implementation 'org.apache.james:apache-mime4j-dom:0.8.9'
    implementation 'io.grpc:grpc-stub:1.56.0'
    implementation 'io.grpc:grpc-okhttp:1.56.0'
    implementation 'javax.annotation:jsr250-api:1.0'
    //noinspection GradleDependency -- no warnning
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    //noinspection GradleDependency
    implementation 'io.grpc:grpc-protobuf-lite:1.0.3'
    //noinspection GradleDependency
    implementation 'commons-io:commons-io:2.13.0'
    //noinspection GradleDependency
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    implementation "androidx.preference:preference:1.2.0"
    implementation 'androidx.exifinterface:exifinterface:1.3.6'

    // AutoValue: https://github.com/google/auto
    implementation 'com.google.auto:auto-common:1.2.2'
    implementation 'com.google.auto.value:auto-value-annotations:1.10.2'
    annotationProcessor 'com.google.auto.value:auto-value:1.10.2'

    // Dagger2: https://github.com/google/dagger
    implementation 'com.google.dagger:dagger:2.46.1'
    annotationProcessor "com.google.dagger:dagger-compiler:2.46.1"

    // App中集成CodeLocator: https://github.com/bytedance/CodeLocator
    debugImplementation "com.bytedance.tools.codelocator:codelocator-core:2.0.3"

    // You need to depend on the lite runtime library, not protobuf-java
    // 见下面的 protobuf，高等级的这个依赖的版本不一样，build脚本也不一样，具体见github
    implementation 'com.google.protobuf:protobuf-lite:3.0.0'
}

protobuf {
    protoc {
        // You still need protoc like in the non-Android case
        if (project.hasProperty('protoc_platform')) {
            artifact = 'com.google.protobuf:protoc:3.7.0:${protoc_platform}'
        } else{
            artifact = 'com.google.protobuf:protoc:3.7.0'
        }
    }
    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.builtins {
                // In most cases you don't need the full Java output
                // if you use the lite output.
                remove java
            }
            task.plugins {
                javalite { }
            }
        }
    }
}
